* Spice library implementing logic gates in NE555 logic (NE)
* 2021-Dec-05  cpldcpu


* Supported gate types:
* NOT
* NAND2
* NAND3
* NOR2
* NOR3
* DFF

* Global nodes:
* VCC       positive supply
* VEE       negative supply

* Global constants
* RL        Load resistor value

* Uses TLC55X model: 
*.SUBCKT TLC55X THRES CONT TRIG RESET OUT DISC VCC GND


.SUBCKT ne_NOT A Y   
XU1 A N001 A VCC Y NC_01 VCC 0 TLC55X
C1 N001 0 1n
C2 VCC 0 100n
.ENDS ne_NOT

.SUBCKT ne_BUF A Y
X1 A B ne_NOT
X2 B Y ne_NOT
.ENDS ne_NOT

.SUBCKT ne_ANDN2 A B Y   
XU1 B N001 B A Y NC_01 VCC 0 TLC55X
C1 N001 0 1n
C2 VCC 0 100n
.ENDS ne_ANDN2

.SUBCKT ne_NAND2 A B Y
XU1 N001 N002 N001 VCC Y NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 VCC N001 {RL}
D1 N001 A RTL_DIODE
D2 N001 B RTL_DIODE
.ENDS ne_NAND2

.SUBCKT ne_NAND3 A B C Y
XU1 N001 N002 N001 VCC Y NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 VCC N001 {RL}
D1 N001 A RTL_DIODE
D2 N001 B RTL_DIODE
D3 N001 C RTL_DIODE
.ENDS ne_NAND3

.SUBCKT ne_NAND4 A B C D Y
XU1 N001 N002 N001 VCC Y NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 VCC N001 {RL}
D1 N001 A RTL_DIODE
D2 N001 B RTL_DIODE
D3 N001 C RTL_DIODE
D4 N001 D RTL_DIODE
.ENDS ne_NAND4


.SUBCKT ne_NOR2 A B Y
XU1 N001 N002 N001 VCC Y NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 N001 0 {RL}
D1 A N001 RTL_DIODE
D2 B N001 RTL_DIODE
.ENDS ne_NOR2

.SUBCKT ne_NOR3 A B C Y
XU1 N001 N002 N001 VCC Y NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 N001 0 {RL}
D1 A N001 RTL_DIODE
D2 B N001 RTL_DIODE
D3 C N001 RTL_DIODE
.ENDS ne_NOR3

.SUBCKT ne_LATCH E D QN
XU1 N001 N002 N001 VCC QN NC_01 VCC 0 TLC55X
C1 N002 0 1n
C2 VCC 0 100n
R1 N001 0 {RL}
Q1 D P001 N001 0 RTL_NPN
R2 VCC N001 {RL}
R3 E P001 {RL}
.ENDS DLATCH

.SUBCKT ne_DFF C D Q
X1 nC D t ne_LATCH
X2 C t Q ne_LATCH
X3 C nC ne_NOT
.ENDS ne_DFF

